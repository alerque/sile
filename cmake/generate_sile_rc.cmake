function(GetGitVersion var)
  execute_process(
    COMMAND git describe --tags
    WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}
    OUTPUT_VARIABLE version)
  string(REPLACE "-" ";" version ${version})
  list(GET version 0 version)
  string(REPLACE "v" "" version ${version})
  set(${var} ${version} PARENT_SCOPE)
endfunction()

function(SplitVersion version major minor build revision)
  string(REPLACE "." ";" version_list ${version})
  list(LENGTH version_list verlen)
  if (verlen GREATER 4 OR verlen LESS 1)
    message(FATAL_ERROR "Incorrect version string: ${version}")
  endif()
  set(${major} 0 PARENT_SCOPE)
  set(${minor} 0 PARENT_SCOPE)
  set(${build} 0 PARENT_SCOPE)
  set(${revision} 0 PARENT_SCOPE)
  if (verlen GREATER_EQUAL 1)
    list(GET version_list 0 version_major)
    set(${major} ${version_major} PARENT_SCOPE)
  endif()
  if (verlen GREATER_EQUAL 2)
    list(GET version_list 1 version_minor)
    set(${minor} ${version_minor} PARENT_SCOPE)
  endif()
  if (verlen GREATER_EQUAL 3)
    list(GET version_list 2 version_build)
    set(${build} ${version_build} PARENT_SCOPE)
  endif()
  if (verlen GREATER_EQUAL 4)
    list(GET version_list 3 version_revision)
    set(${revision} ${version_revision} PARENT_SCOPE)
  endif()
endfunction()

function(GenerateRc template outfile)
   GetGitVersion(VERSION)
   SplitVersion(${VERSION} MAJOR MINOR BUILD REVISION)
   set(ICON_FILE "sile-logo.ico")
   string(TIMESTAMP YEAR "%Y")
   configure_file(${template} ${outfile} @ONLY)
endfunction()